# Example xeno.nu — user macro/hook definitions for Xeno.
#
# Place at ~/.config/xeno/xeno.nu
# Reload with :nu-reload, run with :nu-run <fn> [args...]
#
# Module-only: only def/use/const/alias at top level (no expressions).
#
# Built-in commands available without import:
#   xeno effect dispatch                           — invocation constructor
#   xeno call                           — Nu macro invocation (chaining)
#   "xeno ctx"                         — current invocation context

# --- Hooks ---

# Runs after every action. Return null for no-op.
export def on_action_post [name: string, result: string] {
  null
}

# Runs after every registry command.
# Uses ctx.event for structured event data (no positional-arg dependence).
export def on_command_post [name: string, result: string, ...args: string] {
  let ctx = (xeno ctx)
  if ($ctx.event != null) and ($ctx.event.type == "command_post") and ($ctx.event.data.name == "write") {
    xeno effect notify info "saved!"
  }
}

# Runs after every editor command.
export def on_editor_command_post [name: string, result: string, ...args: string] {
  null
}

# Runs after a mode transition (e.g. Normal → Insert).
export def on_mode_change [from: string, to: string] {
  null
}

# Runs after a buffer is opened or switched to via navigation.
# kind: "disk" (loaded from file) | "existing" (switched to open doc)
export def on_buffer_open [path: string, kind: string] {
  null
}

# --- Macros ---

# Return a list of invocations to execute sequentially.
export def save-and-format [] {
  [(xeno effect dispatch command write), (xeno effect dispatch command format)]
}

# Single invocation with flags.
export def move-down-5 [] {
  xeno effect dispatch action move_down --count 5
}

export def find-x [] {
  xeno effect dispatch action find_char --char x
}

# Chain commands.
export def reload-all [] {
  [(xeno effect dispatch command write), (xeno effect dispatch editor reload_config)]
}

# Context-aware macro using `xeno ctx`.
export def smart-save [] {
  let ctx = (xeno ctx)
  if $ctx.buffer.modified {
    xeno effect dispatch command write
  }
}

# Read cursor position from context.
export def show-position [] {
  let ctx = (xeno ctx)
  let pos = $"($ctx.cursor.line):($ctx.cursor.col)"
  let file = if ($ctx.buffer.path == null) { "<scratch>" } else { $ctx.buffer.path }
  null
}

# Text-aware macro using ctx.text (macro-only, populated with cursor line + selection).
export def toggle-comment [] {
  let ctx = (xeno ctx)
  let line = $ctx.text.line
  if ($line != null) and ($line | str starts-with "#") {
    xeno effect dispatch action uncomment_line
  } else {
    xeno effect dispatch action comment_line
  }
}

# Text edit: upcase the current selection.
export def upcase-selection [] {
  let ctx = (xeno ctx)
  if not $ctx.selection.active { return null }
  let s = $ctx.text.selection
  if $s == null { return null }
  xeno effect edit replace_selection ($s | str upcase)
}

# Text edit: wrap selection in quotes.
export def wrap-quotes [] {
  let ctx = (xeno ctx)
  if not $ctx.selection.active { return null }
  let s = $ctx.text.selection
  if $s == null { return null }
  xeno effect edit replace_selection $"\"($s)\""
}

# Clipboard: copy selection uppercased.
export def copy-upcase-selection [] {
  let ctx = (xeno ctx)
  if not $ctx.selection.active { return null }
  let s = $ctx.text.selection
  if $s == null { return null }
  xeno effect clipboard ($s | str upcase)
}

# Clipboard: copy current file path.
export def copy-file-path [] {
  let ctx = (xeno ctx)
  if $ctx.buffer.path == null { return null }
  xeno effect clipboard $ctx.buffer.path
}

# State: toggle a boolean flag with notification.
export def toggle-foo [] {
  let ctx = (xeno ctx)
  let cur = ($ctx.state | where key == "foo" | get 0?.value)
  let next = if $cur == "1" { "0" } else { "1" }
  [
    (xeno effect state set foo $next),
    (xeno effect notify info $"foo=($next)")
  ]
}

# Schedule: debounced autosave (750ms after last edit).
# Re-scheduling with the same key replaces the previous timer.
export def debounce-save [] {
  xeno effect schedule set autosave 750 save-and-format
}

# Schedule: format after idle (300ms) with args.
export def format-after-idle [] {
  xeno effect schedule set fmt 300 format-buffer --quiet
}

# Schedule: cancel a pending timer.
export def cancel-autosave [] {
  xeno effect schedule cancel autosave
}
